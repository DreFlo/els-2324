grammar org.feup.els5.dsl.TableDSL with org.eclipse.xtext.common.Terminals

generate tableDSL "http://www.feup.org/els5/dsl/TableDSL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Start:
	// TODO Named them actions, could be different
	actions+=CreateTable?
	actions+=TableAction*
;

CreateTable:
	"create"? "table" "from" TableInputPath ANY_OTHER
;

TableAction:
	(LoadData | Operation | Output) ANY_OTHER
;

LoadData:
	"load" TableInputPath
;

TableInputPath:
	pathPatterns+=STRING ("," pathPatterns+=STRING)*
;

Output:
	"output" "to" outputPaths+=STRING ("," outputPaths+=STRING)*
;

Operation:
	Select | RenameColumn | Extract | SquashRows | Filter
;

Select:
	"select" columnsPatterns+=ColumnName ("," columnPatterns+=ColumnName)*
;

RenameColumn:
	"rename" "column"? renameTuples+=RenameColumnPair+
;

RenameColumnPair:
	RenameColumnToPair | RenameColumnAppendPair | RenameColumnPrependPair
;

RenameColumnToPair:
	oldName=STRING "to" newName=STRING
;

RenameColumnPrependPair:
	oldName=STRING "prepend" prefix=STRING
;

RenameColumnAppendPair:
	oldName=STRING "append" suffix=STRING
;

Extract:
	"extract" targets+=ExtractColumnMapping ("," targets+=ExtractColumnMapping)* "from" sourceColumn=STRING "select" selector=Selector "sort" "by"? comparator=Comparator
;

ExtractColumnMapping:
	targetColumn=STRING ("as" newName=STRING)?
;

Selector:
	KeySelector | TopNSelector
;

KeySelector:
	key=SELECTOR_KEYWORDS
;

TopNSelector:
	"TOP" n=INT
;

terminal SELECTOR_KEYWORDS returns ecore::EString:
	"MAX" | "MIN" | "MEDIAN"
;

Comparator:
	keys+=ColumnName ("," keys+=ColumnName)*
;

SquashRows:
	"squash" "rows"? "by" columns+=ColumnName ("," columns+=ColumnName)*
;

Filter:
	"filter" denylist=FilterDenylist exceptlist=FilterExceptlist?
;

FilterDenylist:
	"deny" denylist+=FilterRule ("," denylist+=FilterRule)*
;

FilterExceptlist:
	"except" exceptlist+=FilterRule ("," exceptlist+=FilterRule)*
;

FilterRule:
	FilterColumnRule | FilterObjectTypeRule
;

FilterColumnRule:
	("column" | "columns") columnPatterns+=ColumnName ("," columnPatterns+=ColumnName)*
;

FilterObjectTypeRule:
	("object" | "objects") "of"? objectClasses+=STRING ("," objectClasses+=STRING)*
;

ColumnName:
	columnName=(COLUMN_NAME_KEYWORDS | STRING)
;

terminal COLUMN_NAME_KEYWORDS returns ecore::EString:
	"FILENAME" |
	"DIRECTORY"
;
